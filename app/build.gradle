apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

// Define app version number
ext.versionMajor = 0
ext.versionMinor = 1
ext.versionPatch = 0
ext.versionClassifier = null
ext.isSnapshot = true

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.tiixel.periodictableprofessor"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "com.tiixel.periodictableprofessor.test.TestRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            // Minifying the variant used for tests is not supported when using Jack.
            minifyEnabled false
            // Uses new built-in shrinker http://tools.android.com/tech-docs/new-build-system/built-in-shrinker
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
            testCoverageEnabled true
        }

        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

kapt {
    correctErrorTypes = true
    generateStubs = true
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.2.41"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // App's dependencies, including test
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.lifecycle:runtime:1.1.1"
    implementation "android.arch.persistence.room:runtime:1.1.0"
    implementation "com.android.support.constraint:constraint-layout:1.1.0"
    implementation "com.android.support.test.espresso:espresso-idling-resource:3.0.2"
    implementation "com.android.support:appcompat-v7:27.1.1"
    implementation "com.android.support:cardview-v7:27.1.1"
    implementation "com.android.support:design:27.1.1"
    implementation "com.android.support:recyclerview-v7:27.1.1"
    implementation "com.android.support:support-v4:27.1.1"
    implementation "com.github.daolq3012:AssetSQLiteOpenHelper:1.0"
    implementation "com.google.dagger:dagger-android-support:2.14.1"
    implementation "com.google.dagger:dagger-android:2.14.1"
    implementation "com.google.dagger:dagger:2.14.1"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:2.0.0"
    implementation "com.otaliastudios:zoomlayout:1.1.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    implementation "io.reactivex.rxjava2:rxjava:2.1.7"
    implementation "io.reactivex.rxjava2:rxkotlin:2.2.0"
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.tspoon.traceur:traceur:1.0.1'
    implementation 'org.apache.commons:commons-lang3:3.4'
    implementation 'ru.noties:markwon:1.0.4'
    kapt "android.arch.lifecycle:common-java8:1.1.1"
    kapt "android.arch.persistence.room:compiler:1.1.0"
    kapt "com.google.dagger:dagger-android-processor:2.14.1"
    kapt "com.google.dagger:dagger-compiler:2.14.1"
    compileOnly 'javax.annotation:jsr250-api:1.0'
    implementation 'com.google.code.gson:gson:2.8.2'

    implementation('org.jscience:jscience:4.3.1') {
        exclude(group: 'org.javolution', module: 'javolution')
    }

    // Dependencies for mock
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.3'

    // Dependencies for local unit tests
    testImplementation "android.arch.persistence.room:testing:1.1.0"
    testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
    testImplementation "junit:junit:4.12"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.mockito:mockito-all:1.10.19"
    testImplementation "org.robolectric:robolectric:3.4.2"

    kaptTest "com.google.dagger:dagger-compiler:2.14.1"

    // Android Testing Support Library's runner and rules
    androidTestImplementation "com.android.support.test:rules:0.5"
    androidTestImplementation "com.android.support.test:runner:0.5"

    // Dependencies for Android unit tests
    androidTestImplementation "com.nhaarman:mockito-kotlin:1.5.0"
    androidTestImplementation "junit:junit:4.12"
    androidTestImplementation "org.mockito:mockito-core:1.10.19"
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.2.0'

    kaptAndroidTest "com.google.dagger:dagger-compiler:2.14.1"

    // Espresso UI Testing
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:3.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:3.0.2"
}

private Integer generateVersionCode() {
    return rootProject.ext.minSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null) {
        if (ext.isSnapshot) {
            ext.versionClassifier = "SNAPSHOT"
        }
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName;
}